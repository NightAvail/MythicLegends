#######################################################
# Mythic Legends
# Events
#
# Author:  NightAvail
#######################################################

namespace = ml_events_realm

# Abdicate all titles or all but one title.
character_event = {
    id = "ml_events_realm.1"
    desc = "ml_events_realm.1desc"

    is_triggered_only = yes

    # Give away all titles.
    option = {
        name = "ml_events_realm.1a"
        current_heir = {
            character_event = { id = "ml_events_realm.2" days = 3 random = 3 tooltip = "abd_ask" }
        }
    }

    # Only give the primary title out.
    option = {
        name = "ml_events_realm.1b"
        current_heir = {
            character_event = { id = "ml_events_realm.3" days = 3 random = 3 tooltip = "abd_ask" }
        }
    }

    # This was a mistake!
    option = {
        name = "ml_events_realm.1c"
    }
}

# Give away all titles to your heir.
character_event = {
    id = "ml_events_realm.2"
    desc = "ml_events_realm.2desc"

    is_triggered_only = yes

    option = {
        name = "ml_events_realm.2a"

        ai_chance = {
            factor = 1
        }

        FROM = { character_event = { id = "ml_events_realm.4" } }
    }
}

# Give away all but one title(s) to your heir.
character_event = {
    id = "ml_events_realm.3"
    desc = "ml_events_realm.3desc"

    is_triggered_only = yes

    option = {
        name = "ml_events_realm.3a"

        ai_chance = {
            factor = 1
        }

        FROM = { character_event = { id = "ml_events_realm.5" } }
    }
}

# Abdicate all titles.
character_event = {
    id = "ml_events_realm.4"
    desc = "ml_events_realm.4desc"

    is_triggered_only = yes

    option = {
        name = "ml_events_realm.sbi"

        ai_chance = {
            factor = 1
        }

        FROM = {
            clr_character_flag = abdicate
        }

        abdicate_to = FROM
        FROM = {
             wealth = ROOT
        }
        clear_wealth = yes

        any_claim = {
            remove_claim = ROOT
        }

        # Keep the primary title as a claim for succession.
        FROM = {
            primary_title = {
                add_claim = FROM
            }
        }

        ROOT = { move_character = FROM }
    }
}

# Only give the primary title out.
character_event = {
    id = "ml_events_realm.5"
    desc = "ml_events_realm.5desc"

    is_triggered_only = yes

    option = {
        name = "ml_events_realm.sbi"

        ai_chance = {
            factor = 1
        }

        primary_title = {
            grant_title = FROM
        }
    }
}

# Clear all claims
character_event = {
    id = "ml_events_realm.6"
    desc = "ml_events_realm.6desc"
    hide_window = yes
    is_triggered_only = yes

    option = {
        name = "ml_events_realm.clear_claims"
        any_claim = {
            remove_claim = ROOT
        }
    }
}
